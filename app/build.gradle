apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'realm-android'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolVersion
    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    sourceSets {
        String mainKotlinDir = "src/main/kotlin"
        String sharedTestKotlinDir = "src/shared/kotlin"
        String sharedResourceKotlinDir = "src/shared/res"
        String unitTestKotlinDir = "src/test/kotlin"
        String androidTestKotlinDir = "src/androidTest/kotlin"

        main {
            java.srcDir mainKotlinDir
        }
        test {
            java.srcDir unitTestKotlinDir
            java.srcDir sharedTestKotlinDir
            resources.srcDir sharedResourceKotlinDir
        }
        androidTest {
            java.srcDir androidTestKotlinDir
            java.srcDir sharedTestKotlinDir
            resources.srcDir sharedResourceKotlinDir
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

// Makes sure that kotlin test will not be used in production code
afterEvaluate {
    android.sourceSets.all { sourceSet ->
        if (!sourceSet.name.startsWith("test") || !sourceSet.name.startsWith("androidTest")) {
            sourceSet.kotlin.setSrcDirs([])
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Community Library
    compile "com.jakewharton.timber:timber:$timberLibraryVersion"
    compile "com.squareup.retrofit2:retrofit:$retrofitLibraryVersion"
    compile "io.reactivex.rxjava2:rxkotlin:$rxKotlinLibraryVersion"
    compile "io.reactivex.rxjava2:rxandroid:$rxAndroidLibraryVersion"
    compile "io.reactivex.rxjava2:rxjava:$rxJavaLibraryVersion"
    compile "com.squareup.retrofit2:adapter-rxjava2:$rxJavaRetrofitAdapterLibraryVersion"
    compile "com.google.code.gson:gson:$gsonLibraryVersion"
    compile "com.squareup.retrofit2:converter-gson:$gsonRetrofitConverterLibraryVersion"
    compile "net.danlew:android.joda:$jodaTimeAndroidLibraryVersion"

    // Google Library
    compile "com.android.support:design:$googleDesignLibraryVersion"
    compile "com.android.support:appcompat-v7:$googleSupportLibraryVersion"
    compile "com.android.support.constraint:constraint-layout:$googleContraintLayoutLibraryVersion"
    compile "com.google.android.gms:play-services-maps:$googlePlayServiceLibraryVersion"

    // Test Library
    androidTestCompile "com.google.code.findbugs:jsr305:$jsr305LibraryVersion"
    androidTestCompile("com.android.support.test.espresso:espresso-core:$espressoLibraryVersion", {
        exclude group: "com.android.support", module: "support-annotations"
    })
    testCompile "junit:junit:$junitLibraryVersion"
    testCompile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
    testCompile "joda-time:joda-time:$jodaTimeAndroidLibraryVersion"
    testCompile "org.mockito:mockito-core:$mockitoLibraryVersion"

    // Shared Test Library
    testCompile "com.squareup.okhttp3:mockwebserver:$okhttpLibraryVersion"
    androidTestCompile "com.squareup.okhttp3:mockwebserver:$okhttpLibraryVersion"
    testCompile "org.robolectric:robolectric:$robolectricLibraryVersion"
    androidTestCompile "org.robolectric:robolectric:$robolectricLibraryVersion"

    // Compile time plugin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlinVersion"

}
repositories {
    mavenCentral()
}
